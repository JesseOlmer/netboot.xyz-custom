#!ipxe

# Talos
# https://github.com/siderolabs/talos/releases
# https://www.talos.dev/v1.6/talos-guides/install/bare-metal-platforms/pxe

goto ${menu} ||

:talos
set os Talos
set os_arch ${arch}
iseq ${os_arch} x86_64 && set os_arch amd64 ||
iseq ${os_arch} arm64 && set os_arch arm64 ||
isset ${talos_mirror} || set talos_mirror https://pxe.factory.talos.dev/image
isset ${talos_version} || set talos_version v1.7.5
isset ${talos_factory_image} || set talos_factory_image 70fb923343260bfe3e419cfb7bfeb66719b973df0149da9f49ab7637971b1aef
isset ${talos_platform} || set talos_platform metal
menu ${os} by Talos Systems
menu ${os} install
item --gap Talos:
item talos_boot ${space} Begin install ${os} ${talos_version}
item --gap Parameters:
item talos_version ${space} ${os} version: ${talos_version}
item talos_factory_image ${space} Set factory image id: ${talos_factory_image}
item talos_mirror ${space} Set factory URL: ${talos_mirror}
item talos_platform ${space} Set platform: ${talos_platform}
choose --default ${menu} menu || goto talos_exit
echo ${cls}
goto ${menu} ||
goto talos_exit

:talos_version
menu ${os} version
item latest ${space} latest
item v1.7.5 ${space} v1.7.5
item custom ${space} Set custom version
choose --default ${version} version || goto talos_exit
echo ${cls}
goto talos_version_${version} ||
goto talos_exit

:talos_version_latest
set talos_version latest
goto talos

:talos_version_v1.7.5
set talos_version v1.7.5
goto talos

:talos_version_custom
clear talos_version
echo -n Please set Talos version manually (in format vX.Y.Z):  && read talos_version
clear menu
goto talos

:talos_mirror
echo -n Set factory URL:  && read talos_mirror
clear menu
goto talos

:talos_factory_image
echo -n Set factory image id:  && read talos_factory_image
clear menu
goto talos

# https://www.talos.dev/latest/reference/kernel/#talosplatform
:talos_platform
menu ${os} platforms
item metal ${space} Bare Metal
choose --default ${talos_platform} talos_platform
goto talos

:talos_qemu
set boot_params printk.devkmsg=on slab_nomerge pti=on console=ttyS0 console=tty0 init_on_alloc=1 init_on_free=1 consoleblank=0 nvme_core.io_timeout=4294967295 ima_template=ima-ng ima_appraise=fix ima_hash=sha512 talos.platform=${talos_platform} initrd=initrd.magic ${cmdline}
imgfree
kernel ${talos_mirror}/${talos_factory_image}/${talos_version}/kernel-amd64 talos.platform=${talos_platform} ${boot_params}
initrd ${talos_mirror}/${talos_factory_image}/${talos_version}/initramfs-amd64.xz
echo
echo Booting Kernel ${talos_mirror}/${talos_factory_image}/${talos_version}/kernel-amd64
echo Booting with the following kernel args:
echo ${boot_params}
echo
sleep 3
boot

:talos_exit
clear menu
exit 0
